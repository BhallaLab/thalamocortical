objectvar cell, stim1, vvec, tvec, out_file, child
load_file("nrngui.hoc")
secondorder = 2
define_shape()

create comp[7]
    /*    0 - 1 - 2 - 3 - 5
                   \_ 4 - 6
    */
    comp[1] child = new SectionRef()
    comp[0] connect child.sec(0), 1
    comp[2] child = new SectionRef()
    comp[1] connect child.sec(0), 1
    comp[3] child = new SectionRef()
    comp[2] connect child.sec(0), 1
    comp[4] child = new SectionRef()
    comp[2] connect child.sec(0), 1
    comp[5] child = new SectionRef()
    comp[3] connect child.sec(0), 1
    comp[6] child = new SectionRef()
    comp[4] connect child.sec(0), 1
    comp[0] { L = 20. diam = 2 * 7.5 }
    comp[1] { L = 50. diam = 2 * 0.7 }
    comp[2] { L = 50. diam = 2 * 0.6 }
    comp[3] { L = 50. diam = 2 * 0.5 }
    comp[4] { L = 50. diam = 2 * 0.5 }
    comp[5] { L = 50. diam = 2 * 0.5 }
    comp[6] { L = 50. diam = 2 * 0.5 }
    for i = 0, 6 {
	comp[i] { 
	    insert pas 
	    cm = 0.9
	}
    }
    comp[0]{
	g_pas = 2e-5
	Ra = 100
	insert naf2
	 gbar_naf2 =   0.15
	 insert napf_spinstell
	 gbar_napf_spinstell =   0.00015
	 insert kdr_fs
	 gbar_kdr_fs =   0.1
	 insert kc_fast
	 gbar_kc_fast =   0.01
	 insert ka
	 gbar_ka =   0.03
	 insert km
	 gbar_km =   0.00375
	 insert k2
	 gbar_k2 =   0.0001
	 insert kahp_slower
	 gbar_kahp_slower =   0.0001
	 insert cal
	 gbar_cal =   0.0005
	 insert cat_a
	 gbar_cat_a =   0.0001
	 insert ar
	 gbar_ar =   0.00025
	 insert cad
	 // *** ca diffusion: beta=1/tau
	 beta_cad  =   0.02
	 // cafor(I) (FORTRAN) converted to phi (NEURON)
	 phi_cad =   260000.
     }
     
     for i = 1, 6 {
	 comp[i] {
	 g_pas = 0.001
	 Ra = 100
	 insert naf2
	 gbar_naf2 =   0.4
	 insert kdr_fs
	 gbar_kdr_fs =   0.4
	 insert ka
	 gbar_ka =   0.002
	 insert k2
	 gbar_k2 =   0.0001	 
	 }
     }
     for i = 0, 6 {
	 comp[i]{
             if (ismembrane("naf2")) {fastNa_shift_naf2=-2.5}
	     // global reversal potentials
	     ek =  -100.
	     e_pas =  -65.
	     ena =   50.
	     vca =   125.
	     if (ismembrane("ar")){ erev_ar =  -40. m0_ar = 0 }
	     e_gaba_a =  -75.
	     if (ismembrane("napf_spinstell")) {fastNa_shift_napf_spinstell=-2.5}
	 }
     }
     access comp[0]
     stim1 = new IClamp(0.5)
     stim1.del = 20
     stim1.amp = 0.3
     stim1.dur = 20
     for i = 0, 6 { comp[i] v = -65. }
vvec = new Vector()
tvec = new Vector()
vvec.record(&comp[0].v(0.5))
tvec.record(&t)
tstop = 50
run()
out_file = new File()
out_file.wopen("mydata/Vm.dat")
for ii = 0, vvec.size() - 1 {
    out_file.printf("%g %g\n", tvec.x(ii), vvec.x(ii))
}
out_file.close()
