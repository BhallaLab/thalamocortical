// Test for NMDA channel in Traub et al 2005 using two passive
// compartments.
// Subhasis Ray. 2009-2012
objref netcon, conobj, syn
objectvar vecT, vecV_a, vecV_b, stim1, vecG_a, vecG_b, vecMg_unblocked, outfile
outfile = new File()
vecV_a = new Vector()
vecV_b = new Vector()
vecG_a = new Vector()
vecG_b = new Vector()
vecMg_unblocked = new Vector()
vecT = new Vector()

FARADAY=96485
PI=3.141592
secondorder=2
dt=1e-2
celsius=30
v_init=-65

create soma_a
access soma_a
soma_a{
    nseg = 1
    diam = 15
    L = 20
    
    insert pas
    e_pas = -65
    g_pas = 2e-5
    Ra = 250.0
    cm = 0.9
    
    stim1 = new IClamp(0.5)
    stim1.amp = 0.1
    stim1.dur = 20
    stim1.del = 20
}

create soma_b
access soma_b
soma_b{
    nseg = 1
    diam = 15
    L = 20
    insert pas
    e_pas = -65
    g_pas = 2e-5
    Ra = 250.0
    cm = 0.9
    
    syn = new NMDA(0.5)
    syn.tau = 130.5 // ms    
    // syn.weight = 1e2 // uS
    // syn.NMDA_saturation_fact = 0.25
}
// new NetCon(&source_v, synapse, threshold, delay, weight)
soma_a netcon = new NetCon(&v(1), syn, 0.0, 0.05, 0.25e-3)
// netcon.weight = 0.025e-3 // uS
// netcon.delay = 0.05 // ms
// netcon.threshold = 0.0

tstop = 1000.0
vecV_a.record(&soma_a.v(0.5))
vecV_b.record(&soma_b.v(0.5))
// NOTE: g is not effective conductance. Effective conductance is g * Mg_unblocked.
vecG_b.record(&syn.g) 
vecMg_unblocked.record(&syn.Mg_unblocked)
vecT.record(&t)
run()
outfile.wopen("mydata/two_comp_nmda.plot")
outfile.printf("# time presynaptic_vm postsynaptic_vm synaptic_conductance\n")
for ii = 0, vecV_a.size() - 1 {
	outfile.printf("%g %g %g %g\n", vecT.x(ii)*1e-3, vecV_a.x(ii)*1e-3, vecV_b.x(ii)*1e-3, vecG_b.x(ii)*vecMg_unblocked.x(ii)*1e-6)
}
outfile.close()
quit()
